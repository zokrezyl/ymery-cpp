plugins {
    id 'com.android.application'
}

android {
    namespace 'com.ymery.app'
    compileSdk 34
    ndkVersion '26.1.10909125'

    defaultConfig {
        applicationId "com.ymery.app"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters 'arm64-v8a'
        }

        externalNativeBuild {
            cmake {
                arguments '-DYMERY_ANDROID=ON',
                          '-DCMAKE_POLICY_VERSION_MINIMUM=3.5'
                cppFlags '-std=c++2b'
            }
        }
    }

    // Product flavors for OpenGL and WebGPU backends
    flavorDimensions = ["backend"]
    productFlavors {
        opengl {
            dimension "backend"
            applicationIdSuffix ".opengl"
            versionNameSuffix "-opengl"
            externalNativeBuild {
                cmake {
                    arguments '-DYMERY_USE_WEBGPU=OFF'
                }
            }
        }
        webgpu {
            dimension "backend"
            applicationIdSuffix ".webgpu"
            versionNameSuffix "-webgpu"
            externalNativeBuild {
                cmake {
                    arguments '-DYMERY_USE_WEBGPU=ON'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
            buildStagingDirectory new File(rootProject.projectDir.parentFile.parentFile, "build-android/cxx")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
}

// Copy APK to build-android-{flavor}-{buildType} at repo root
def repoRoot = rootProject.projectDir.parentFile.parentFile.parentFile
android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        def flavorName = variant.flavorName
        def buildType = variant.buildType.name
        def outputDir = new File(repoRoot, "build-android-${flavorName}-${buildType}")

        variant.assembleProvider.get().doLast {
            outputDir.mkdirs()
            def apkFile = output.outputFile
            if (apkFile.exists()) {
                copy {
                    from apkFile
                    into outputDir
                }
                println "APK copied to: ${outputDir}/${apkFile.name}"
            }
        }
    }
}
